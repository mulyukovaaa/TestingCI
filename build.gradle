plugins {
    id 'java'
    id 'jacoco'
    id'io.qameta.allure' version '2.10.0'
}

group = 'org.usvm'
version = '1.0-SNAPSHOT'

def allureVersion = '2.29.0'
def aspectJVersion = '1.9.22'

repositories {
    mavenCentral()
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    implementation("io.javalin:javalin:6.4.0")
    implementation("org.slf4j:slf4j-simple:2.0.16")
    implementation("com.google.code.gson:gson:2.12.1")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation("io.rest-assured:rest-assured:5.5.1")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("org.mockito:mockito-core:5.15.2")

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation 'io.qameta.allure:allure-junit5'
    testImplementation 'io.qameta.allure:allure-selenide'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('jacocoReports')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
        xml.outputLocation = layout.buildDirectory.file('jacocoReports/test/jacocoTestReport.xml')
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
